MATCH p = allShortestPaths((person1:Person)-[:KNOWS*0..10]-(person2:Person))
WHERE id(person1) == $person1Id AND id(person2) == $person2Id
WITH collect(p) as paths
UNWIND paths as path1
WITH path1, relationships(path1) as rels_in_path
WITH
    [n in nodes(path1) | id(n) ] as personIdsInPath,
    [r in rels_in_path |
        reduce(w=0.0, v in [
            (a:Person)<-[:HAS_CREATOR]-(:Comment)-[:REPLY_OF]->(:Post)-[:HAS_CREATOR]->(b:Person)
            WHERE
                (id(a) == id(startNode(r)) and id(b) == id(endNode(r))) OR (id(a)== id(endNode(r)) and id(b) == id(startNode(r)))
            | 1.0] | w+v)
    ] as weight1,
    [r in rels_in_path |
        reduce(w=0.0,v in [
        (a:Person)<-[:HAS_CREATOR]-(:Comment)-[:REPLY_OF]->(:Comment)-[:HAS_CREATOR]->(b:Person)
        WHERE
                (id(a) == id(startNode(r)) and id(b) == id(endNode(r))) OR (id(a)== id(endNode(r)) and id(b) == id(startNode(r)))
        | 0.5] | w+v)
    ] as weight2
WITH
    personIdsInPath,
    reduce(w=0.0,v in weight1| w+v) as w1,
    reduce(w=0.0,v in weight2| w+v) as w2
RETURN
    personIdsInPath,
    (w1+w2) as pathWeight
ORDER BY pathWeight desc